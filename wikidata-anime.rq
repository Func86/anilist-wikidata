SELECT (MIN(xsd:integer(?value)) AS ?id)
       (SAMPLE(?type) AS ?type)
       ?source
       ?lang
       (SAMPLE(?label) AS ?title)
       (SAMPLE(?finalPage) AS ?page)
       (SAMPLE(?finalPageId) AS ?pageId)
       (SAMPLE(?dateModified) AS ?dateModified)
WHERE {{
  BIND('anime' AS ?type)
  {
    ?item p:P8729 ?prop.
    ?prop rdf:type wikibase:BestRank;
          ps:P8729 ?value.
    OPTIONAL { ?prop pq:P1545 ?index. }

    OPTIONAL { ?item wdt:P364 ?originalLanguage. }
    FILTER(?originalLanguage = wd:Q5287 || !BOUND(?originalLanguage))

    ?item rdfs:label ?label.
    BIND(LANG(?label) AS ?lang)
    FILTER(STRSTARTS(?lang, "zh") || ?lang = "en")

    ?item schema:dateModified ?dateModified.
    BIND(0 AS ?source).
  } UNION {
    ?item p:P8729 ?prop.
    ?prop rdf:type wikibase:BestRank;
          ps:P8729 ?value.
    OPTIONAL { ?prop pq:P1545 ?index. }

    OPTIONAL { ?item wdt:P364 ?originalLanguage. }
    FILTER(?originalLanguage = wd:Q5287 || !BOUND(?originalLanguage))

    # In this branch of the UNION query, we only want items with a P144 (origin) statement, but somehow,
    # keeping these inside an OPTIONAL block can make the query way faster, from >20s to 6-8s
    OPTIONAL {
      ?item wdt:P144 ?origin.
      ?origin schema:dateModified ?dateModified.
      SERVICE wikibase:label {
        bd:serviceParam wikibase:language "zh,zh-hans,zh-hant,zh-cn,zh-tw,zh-hk,zh-sg,zh-mo,zh-my,en".
        ?item rdfs:label ?autoLabel.
        ?origin rdfs:label ?originLabel.
      }
      SERVICE wikibase:label {
        bd:serviceParam wikibase:language "en".
        ?item rdfs:label ?enLabel.
        ?origin rdfs:label ?originEnLabel.
      }
    }

    # Query the title variants of the origin entity, when all of the following conditions are met:
    # 1. The title of the item is in English or undefined
    # 2. The title of the origin entity is in Chinese
    # 3. The English title of the item is the same as the English title of the origin entity
    FILTER(!STRSTARTS(LANG(?autoLabel), "zh") && STRSTARTS(LANG(?originLabel), "zh") && LCASE(?enLabel) = LCASE(?originEnLabel))

    # We have to keep these inside the UNION query because it would timeout/OOM when querying rdfs:label
    # on variables that are bounded with BIND, even if it's as simple as `BIND(?item AS ?itemCopy)`.
    ?origin rdfs:label ?label.
    BIND(LANG(?label) AS ?lang)
    FILTER(STRSTARTS(?lang, "zh"))
    BIND(1 AS ?source).
  } UNION {
    ?item p:P8729 ?prop.
    ?prop rdf:type wikibase:BestRank;
          ps:P8729 ?value.
    OPTIONAL { ?prop pq:P1545 ?index. }

    OPTIONAL { ?item wdt:P364 ?originalLanguage. }
    FILTER(?originalLanguage = wd:Q5287 || !BOUND(?originalLanguage))

    # Query the title of the series, if the anime is the first season of it
    ?item p:P179 [
      pq:P1545 "1";
      ps:P179 ?series;
    ].

    {
      ?series rdfs:label ?label.
      BIND(LANG(?label) AS ?lang).
      FILTER(STRSTARTS(?lang, "zh")).

      ?series schema:dateModified ?dateModified.
      BIND(2 AS ?source).
    } UNION {
      ?series wdt:P144 ?origin.

      # Similar to the branch above, but for the series
      SERVICE wikibase:label {
        bd:serviceParam wikibase:language "zh,zh-hans,zh-hant,zh-cn,zh-tw,zh-hk,zh-sg,zh-mo,zh-my,en".
        ?series rdfs:label ?autoLabel.
        ?origin rdfs:label ?originLabel.
      }
      SERVICE wikibase:label {
        bd:serviceParam wikibase:language "en".
        ?series rdfs:label ?enLabel.
        ?origin rdfs:label ?originEnLabel.
      }

      FILTER(!STRSTARTS(LANG(?autoLabel), "zh") && STRSTARTS(LANG(?originLabel), "zh") && LCASE(?enLabel) = LCASE(?originEnLabel))

      ?origin rdfs:label ?label.
      BIND(LANG(?label) AS ?lang).
      FILTER(STRSTARTS(?lang, "zh")).

      ?origin schema:dateModified ?dateModified.
      BIND(3 AS ?source).
    }
  }

  OPTIONAL {
    ?item p:P5737 ?pageStatement.
    ?pageStatement rdf:type wikibase:BestRank;
                   ps:P5737 ?page;
                   pq:P9675 ?pageId.
  }

  OPTIONAL {
    ?item wdt:P179 ?series
    OPTIONAL {
      ?series p:P5737 ?seriesPageStatement.
      ?seriesPageStatement rdf:type wikibase:BestRank;
                           ps:P5737 ?seriesPage;
                           pq:P9675 ?seriesPageId.
    }
    OPTIONAL {
      ?series wdt:P144/p:P5737 ?seriesOriginPageStatement.
      ?seriesOriginPageStatement rdf:type wikibase:BestRank;
                                 ps:P5737 ?seriesOriginPage;
                                 pq:P9675 ?seriesOriginPageId.
      OPTIONAL { ?seriesOriginPageStatement pq:P407 ?seriesOriginPageLang }
      FILTER(!BOUND(?seriesOriginPageLang) || ?seriesOriginPageLang = wd:Q7850)
    }
    # The link to the media mix page of the series
    OPTIONAL {
      ?series wdt:P8345/p:P5737 ?seriesMedmixPageStatement.
      ?seriesMedmixPageStatement rdf:type wikibase:BestRank;
                                 ps:P5737 ?seriesMedmixPage;
                                 pq:P9675 ?seriesMedmixPageId.
    }
  }

  OPTIONAL {
    ?item wdt:P144 ?origin
    OPTIONAL {
      ?origin p:P5737 ?originPageStatement.
      ?originPageStatement rdf:type wikibase:BestRank;
                           ps:P5737 ?originPage;
                           pq:P9675 ?originPageId.
      # Filter out pages in non-Chinese languages
      OPTIONAL { ?originPageStatement pq:P407 ?originPageLang }
    }
    FILTER(!BOUND(?originPageLang) || ?originPageLang = wd:Q7850)
  }

  OPTIONAL {
    ?item wdt:P8345/p:P5737 ?medmixPageStatement.
    ?medmixPageStatement rdf:type wikibase:BestRank;
                         ps:P5737 ?medmixPage;
                         pq:P9675 ?medmixPageId.
    }

  BIND(COALESCE(?page, ?seriesPage, ?seriesOriginPage, ?originPage, ?medmixPage, ?seriesMedmixPage) AS ?finalPage)
  BIND(COALESCE(?pageId, ?seriesPageId, ?seriesOriginPageId, ?originPageId, ?medmixPageId, ?seriesMedmixPageId) AS ?finalPageId)
  FILTER(STRSTARTS(?lang, "zh") || BOUND(?finalPage))
}}
GROUP BY ?item ?source ?lang ?index
ORDER BY ?id ?source ?lang
