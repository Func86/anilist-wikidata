SELECT (MIN(xsd:integer(?value)) AS ?id)
       ?isAnime
       ?source
       ?lang
       (SAMPLE(?label) AS ?title)
       (SAMPLE(?finalPage) AS ?page)
       (SAMPLE(?dateModified) AS ?dateModified)
WHERE {{
  BIND(true AS ?isAnime)
  {
    ?item wdt:P8729 ?value.

    OPTIONAL { ?item wdt:P364 ?originalLanguage. }
    FILTER(?originalLanguage = wd:Q5287 || !BOUND(?originalLanguage))

    ?item rdfs:label ?label.
    BIND(LANG(?label) AS ?lang)
    FILTER(STRSTARTS(?lang, "zh") || ?lang = "en")

    ?item schema:dateModified ?dateModified.
    BIND(0 AS ?source).
  } UNION {
    ?item wdt:P8729 ?value.

    OPTIONAL { ?item wdt:P364 ?originalLanguage. }
    FILTER(?originalLanguage = wd:Q5287 || !BOUND(?originalLanguage))

    # In this branch of the UNION query, we only want items with a P144 (origin) statement, but somehow,
    # keeping these inside an OPTIONAL block can make the query way faster, from >20s to 6-8s
    OPTIONAL {
      ?item wdt:P144 ?origin.
      ?origin schema:dateModified ?dateModified.
      SERVICE wikibase:label {
        bd:serviceParam wikibase:language "zh,zh-hans,zh-hant,zh-cn,zh-tw,zh-hk,zh-sg,zh-mo,zh-my,en".
        ?item rdfs:label ?autoLabel.
        ?origin rdfs:label ?originLabel.
      }
      SERVICE wikibase:label {
        bd:serviceParam wikibase:language "en".
        ?item rdfs:label ?enLabel.
        ?origin rdfs:label ?originEnLabel.
      }
    }

    # Query the title variants of the origin entity, when all of the following conditions are met:
    # 1. The title of the item is in English or undefined
    # 2. The title of the origin entity is in Chinese
    # 3. The English title of the item is the same as the English title of the origin entity
    FILTER(!STRSTARTS(LANG(?autoLabel), "zh") && STRSTARTS(LANG(?originLabel), "zh") && LCASE(?enLabel) = LCASE(?originEnLabel))

    # We have to keep these inside the UNION query because it would timeout/OOM when querying rdfs:label
    # on variables that are bounded with BIND, even if it's as simple as `BIND(?item AS ?itemCopy)`.
    ?origin rdfs:label ?label.
    BIND(LANG(?label) AS ?lang)
    FILTER(STRSTARTS(?lang, "zh"))
    BIND(1 AS ?source).
  } UNION {
    ?item wdt:P8729 ?value.

    OPTIONAL { ?item wdt:P364 ?originalLanguage. }
    FILTER(?originalLanguage = wd:Q5287 || !BOUND(?originalLanguage))

    # Query the title of the series, if the anime is the first season of it
    ?item p:P179 [
      pq:P1545 "1";
      ps:P179 ?series;
    ].

    {
      ?series rdfs:label ?label.
      BIND(LANG(?label) AS ?lang).
      FILTER(STRSTARTS(?lang, "zh")).

      ?series schema:dateModified ?dateModified.
      BIND(2 AS ?source).
    } UNION {
      ?series wdt:P144 ?origin.

      # Similar to the branch above, but for the series
      SERVICE wikibase:label {
        bd:serviceParam wikibase:language "zh,zh-hans,zh-hant,zh-cn,zh-tw,zh-hk,zh-sg,zh-mo,zh-my,en".
        ?series rdfs:label ?autoLabel.
        ?origin rdfs:label ?originLabel.
      }
      SERVICE wikibase:label {
        bd:serviceParam wikibase:language "en".
        ?series rdfs:label ?enLabel.
        ?origin rdfs:label ?originEnLabel.
      }

      FILTER(!STRSTARTS(LANG(?autoLabel), "zh") && STRSTARTS(LANG(?originLabel), "zh") && LCASE(?enLabel) = LCASE(?originEnLabel))

      ?origin rdfs:label ?label.
      BIND(LANG(?label) AS ?lang).
      FILTER(STRSTARTS(?lang, "zh")).

      ?origin schema:dateModified ?dateModified.
      BIND(3 AS ?source).
    }
  }

  OPTIONAL { ?item wdt:P5737 ?page }

  OPTIONAL {
    ?item wdt:P179 ?series
    OPTIONAL { ?series wdt:P5737 ?seriesPage }
    OPTIONAL {
      ?series wdt:P144/p:P5737 ?seriesOriginPageStatement.
      ?seriesOriginPageStatement ps:P5737 ?seriesOriginPage.
      OPTIONAL { ?seriesOriginPageStatement pq:P407 ?seriesOriginPageLang }
      FILTER(!BOUND(?seriesOriginPageLang) || ?seriesOriginPageLang = wd:Q7850)
    }
    # The link to the media mix page of the series
    OPTIONAL { ?series wdt:P8345/wdt:P5737 ?seriesMedmixPage }
  }

  OPTIONAL {
    ?item wdt:P144 ?origin
    OPTIONAL {
      ?origin p:P5737 ?originPageStatement.
      ?originPageStatement ps:P5737 ?originPage.
      # Using "p:" and then "ps:" instead of "wdt:" would cause the deprecated ones to be included,
      # so we need to filter them out manually
      ?originPageStatement wikibase:rank ?originPageRank
      # Filter out pages in non-Chinese languages
      OPTIONAL { ?originPageStatement pq:P407 ?originPageLang }
    }
    FILTER(?originPageRank != wikibase:DeprecatedRank && (!BOUND(?originPageLang) || ?originPageLang = wd:Q7850))
    # Somehow querying the media mix of the series would cause the order of the results to be different,
    # so we need to query more specifically before fallback to the origin page
    OPTIONAL { ?origin wdt:P361/wdt:P5737 ?originEntityPage }
  }

  OPTIONAL { ?item wdt:P8345/wdt:P5737 ?medmixPage }

  BIND(COALESCE(?page, ?seriesPage, ?seriesOriginPage, ?originEntityPage, ?originPage, ?medmixPage, ?seriesMedmixPage) AS ?finalPage)
  FILTER(STRSTARTS(?lang, "zh") || BOUND(?finalPage))
} UNION {
  ?item wdt:P8731 ?value.
  BIND(false AS ?isAnime)

  OPTIONAL { ?item wdt:P364 ?originalLanguage. }
  FILTER(?originalLanguage = wd:Q5287 || !BOUND(?originalLanguage))

  ?item rdfs:label ?label.
  BIND(LANG(?label) AS ?lang)
  FILTER(STRSTARTS(?lang, "zh"))

  ?item schema:dateModified ?dateModified.
}}
GROUP BY ?isAnime ?item ?source ?lang
ORDER BY DESC(?isAnime) ?id ?source ?lang
